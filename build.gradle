plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group 'com.softwareverde'
version '0.0.0'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation  group: 'com.softwareverde',  name: 'java-logging',  version: 'v2.3.0'

    api  group: 'com.softwareverde',        name: 'java-util',              version: 'v2.7.3'
    api  group: 'com.softwareverde',        name: 'java-concurrent',        version: 'v1.0.1'
    api  group: 'com.github.softwareverde', name: 'java-cryptography',      version: 'v3.2.1'
    api  group: 'com.github.softwareverde', name: 'json',                   version: 'v2.0.0'
    api  group: 'com.softwareverde',        name: 'java-db',                version: 'v3.3.0'

    api (group: 'com.github.softwareverde', name: 'java-http-client') {     version { strictly 'v2.2.5' } }
    api  group: 'com.softwareverde',        name: 'http-servlet',           version: 'v3.3.1'
    api  group: 'com.softwareverde',        name: 'java-socket',            version: 'v0.2.3'
    // implementation  group: 'com.softwareverde',  name: 'bitcoin-verde', version: '460bd5a1'

    implementation group: 'com.github.softwareverde',     name: 'java-db-mysql',          version: 'v4.2.2'
    implementation group: 'com.github.softwareverde',     name: 'java-db-mysql-embedded', version: 'v3.0.3'
    implementation group: 'org.mariadb.jdbc',             name: 'mariadb-java-client',    version: '2.7.3'

    implementation group: 'com.github.softwareverde',     name: 'java-mariadb-osx',       version: 'v10.5.9-p4'
    implementation group: 'com.github.softwareverde',     name: 'java-mariadb-linux',     version: 'v10.5.9-p1'
    implementation group: 'com.github.softwareverde',     name: 'java-mariadb-windows',   version: 'v10.5.9'
    implementation group: 'org.apache.commons',           name: 'commons-dbcp2',          version: '2.8.0'

    implementation group: 'org.jocl',                     name: 'jocl',                   version: '2.0.1'

    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

task copyDependencies(type: Copy) {
    from (configurations.compileClasspath + configurations.runtimeClasspath)
    into "$buildDir/libs/libs"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task makeJar(type: Jar) {
    manifest {
        attributes  'Implementation-Title': 'Monetize Dot Cash Server',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'com.softwareverde.monetize.Main',
                'Class-Path': (project.configurations.compileClasspath + configurations.runtimeClasspath).collect { 'libs/' + it.getName() }.join(' ')
    }

    archiveBaseName = project.name

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output

    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}